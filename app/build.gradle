apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: "kotlin-kapt"

apply plugin: 'androidx.navigation.safeargs'

apply plugin: "de.mannodermaus.android-junit5"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.syffer.pincreminder"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        // use the runner of androidx
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // connect junit5 to the runner
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    // DataBindingUtils disabled by default
    dataBinding {
        enabled = true
    }
    // to be able to use navArgs()
    // avoid "Cannot inline bytecode built with JVM target 1.8 into bytecode that is being built with JVM target 1.6"
    // @see https://stackoverflow.com/questions/48988778/cannot-inline-bytecode-built-with-jvm-target-1-8-into-bytecode-that-is-being-bui
    kotlinOptions {
        jvmTarget = "1.8"
    }
    // to be able to use Robolectric (for testing android api without launching device)
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    // @see https://developer.android.com/jetpack/androidx/migrate
    // @see https://developer.android.com/jetpack/androidx/migrate/class-mappings

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation "com.google.android.material:material:1.0.0-rc01" // 'com.android.support:design:28.0.0'

    // local tests
    // @see https://developerlife.com/2018/10/21/kotlin-testing-setup/
    // @see https://github.com/mannodermaus/android-junit5
    def junit_version = "5.5.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junit_version"
    def junit4_version = "4.12"
    testImplementation "junit:junit:$junit4_version"
    // @see https://github.com/fada21/AndroidTestContextExample/blob/master/app/build.gradle
    testImplementation "androidx.test:core:1.1.0"
    testImplementation "androidx.test:runner:1.1.1"
    testImplementation "androidx.test.ext:junit:1.1.0"
    testImplementation "androidx.test.ext:truth:1.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1"
    def robolectric_version = "4.3"
    testImplementation "org.robolectric:robolectric:$robolectric_version"

    // android tests
    // - the instrumentation test companion libraries, needed for junit 5
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.1.0"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.1.0"

    // ORM
    def room_version = "2.1.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version" // optional - Kotlin Extensions and Coroutines support
    kapt "androidx.room:room-compiler:$room_version"

    // life cycle
    def lifecycle_version = "2.1.0-rc01"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version" // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version" // ViewModelScope in there
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    testImplementation "androidx.arch.core:core-testing:$lifecycle_version" // tests helpers for LiveData

    // data binding
    kapt "com.android.databinding:compiler:$gradle_version"

    // navigation
    def nav_version = "2.1.0-rc01"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // coroutines
    def coroutine_version = "1.3.0-RC"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutine_version"
}
